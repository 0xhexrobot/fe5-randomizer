plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

application {
    mainModule = 'org.hexrobot.fe5randomizer'
    mainClass = 'org.hexrobot.fe5randomizer.Launcher'
}

// read version
Properties properties = new Properties()
File propertiesFile = new File('src/main/resources/org/hexrobot/fe5randomizer/version.properties')

propertiesFile.withInputStream {
    properties.load(it)
}

version = properties['version']

repositories {
    mavenCentral()
}

javafx {
    version = "14"
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    // https://mvnrepository.com/artifact/org.freemarker/freemarker
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        /**/
    }

    jpackage {
        installerOptions += '--verbose'
        vendor = 'hexrobot'

        def iconPath = 'src/main/resources/org/hexrobot/fe5randomizer'
        def currentOs = org.gradle.internal.os.OperatingSystem.current()

        if(currentOs.macOsX) {
            installerType = 'dmg'
            imageOptions += ['--icon', "${iconPath}/launcher.icns"]
            installerOptions += ['--resource-dir', "build/resources/main/org/hexrobot/fe5randomizer",]
        }

        if (currentOs.linux) {
            installerType = 'deb'
            imageOptions += ['--icon', "${iconPath}/launcher.png"]
            installerOptions += [
                    '--resource-dir', "build/jpackage/${name}/lib",
                    '--linux-menu-group', 'Game',
                    '--description', 'Fire Emblem 5 Thracia 776 Randomizer by hexrobot.',
                    '--app-version', version,
                    '--linux-shortcut'
            ]
        }

        if (currentOs.windows) {
            installerType = 'exe'
            imageOptions += ['--icon', "${iconPath}/launcher.ico"]
            installerOptions += ['--win-per-user-install',
                                 '--win-dir-chooser',
                                 '--win-menu','--win-shortcut'
            ]
        }
    }
}
